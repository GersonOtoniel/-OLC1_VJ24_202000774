<INIT> ::= <instrucciones>

<instrucciones> ::= <instrucciones> <instruccion>
                  | <instruccion>

<instruccion> ::= <DECLARACION> ";"
                | <ASIGNACION> ";"
                | <INC_DECINS> ";"
                | <IF>
                | <INSFOR>
                | <BREAK> ";"
                | <CONTINUE> ";"
                | <MATCHINS>
                | <INSWHILE>
                | <DOWHILEINS> ";" 
                | <PRINT> ";"
                | <error> 

<INC_DECINS> ::= "ID" "INC"
               | "ID" "DEC"

<DECLARACION> ::= <MUTACION> "ID" ":" <TIPO> "=" <EXPRESION>
                | <MUTACION> "ID" ":" <TIPO>

<ASIGNACION> ::= "ID" "=" <EXPRESION>

<PRINT> ::= "PRINTLN" "(" <EXPRESION> ")"

<IF> ::= "TKIF" "(" <EXPRESION> ")" <BLOCKINS>
       | "TKIF" "(" <EXPRESION> ")" <BLOCKINS> "ELSE" <BLOCKINS>
       | "TKIF" "(" <EXPRESION> ")" <BLOCKINS> "ELSE" <IF>

<MATCHINS> ::= "MATCH" <EXPRESION> "{" <BLOCKCASES> "}"

<BLOCKCASES> ::= <CASES> <DEFAULTINS>
               | <CASES>
               | <DEFAULTINS>

<CASES> ::= <CASES> <CASE>
          | <CASE>

<CASE> ::= <EXPRESION> "ARROW" "{" <instrucciones> "}"

<DEFAULTINS> ::= "DEFAULT" "ARROW" "{" <instrucciones> "}"

<INSWHILE> ::= "WHILE" "(" <EXPRESION> ")" <BLOCKINS>

<DOWHILEINS> ::= "DO" <BLOCKINS> "WHILE" "(" <EXPRESION> ")"

<INSFOR> ::= "FOR" "(" <IDFOR> ";" <EXPRESION> ";" <EXPRESIONFOR> ")" <BLOCKINS>

<EXPRESIONFOR> ::= <ASIGNACION>
                 | <INC_DECINS>

<IDFOR> ::= "ID" "=" <EXPRESION>

<BLOCKINS> ::= "{" <instrucciones> "}"

<BREAK> ::= "TKBREAK"

<CONTINUE> ::= "TKCONTINUE"

<MUTACION> ::= "VAR"
             | "CONST"

<TIPO> ::= "CHAR"
         | "DOUBLE"
         | "INT"
         | "BOOL"
         | "TKSTRING"

<EXPRESION> ::= <INCRE_DECRE>
              | <EXPRESION> "%" <EXPRESION>
              | <EXPRESION> "+" <EXPRESION>
              | <EXPRESION> "-" <EXPRESION>
              | <EXPRESION> "*" <EXPRESION>
              | <EXPRESION> "/" <EXPRESION>
              | <EXPRESION> "**" <EXPRESION>
              | <EXPRESION> "==" <EXPRESION>
              | <EXPRESION> "!=" <EXPRESION>
              | <EXPRESION> "<" <EXPRESION>
              | <EXPRESION> ">" <EXPRESION>
              | <EXPRESION> ">=" <EXPRESION>
              | <EXPRESION> "<=" <EXPRESION>
              | <EXPRESION> "AND" <EXPRESION>
              | <EXPRESION> "XOR" <EXPRESION>
              | <EXPRESION> "OR" <EXPRESION>
              | "NOT" <EXPRESION>
              | "MENOS" <EXPRESION>
              | "(" <EXPRESION> ")"
              | "(" <TIPO> ")" <EXPRESION>
              | "STRING"
              | "ID"
              | "DECIMAL"
              | "ENTERO"
              | "TRUE"
              | "FALSE"
              | "TKCHAR"

<INCRE_DECRE> ::= "ID" "INC"
                | "ID" "DEC"
